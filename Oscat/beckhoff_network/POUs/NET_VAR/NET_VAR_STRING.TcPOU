<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.9">
  <POU Name="NET_VAR_STRING" Id="{192c1f0d-c17f-4abd-bc83-96e1fc31e174}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK NET_VAR_STRING
VAR_IN_OUT
	IN : T_MaxString;
	OUT : T_MaxString;
	X : NET_VAR_DATA;
END_VAR
VAR_OUTPUT
	ID : BYTE;
END_VAR
VAR
	init : BOOL;
	len_str : INT;
	index : INT;
	index2 : INT;
END_VAR

(*
version 1.0		15. may. 2011
programmer		ks
tested by			ks

NET_VAR_STRING überträgt bidirektional einen STRING vom Master zum Client
*)
]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF NOT init THEN
	init := TRUE;
	X.ID_MAX := X.ID_MAX + USINT#1;
	ID := USINT_TO_BYTE(X.ID_MAX);
END_IF;

IF X.STATE > BYTE#0 AND X.ERROR_ID = BYTE#0 THEN
	index := X.index; (* PCWORX Workaround *)
	IF X.STATE = BYTE#1 THEN (* Daten schreiben *)
		len_str := LEN(IN);
		IF TO_UINT(index + 3 + len_str) > X.BUF_SIZE THEN
			X.ERROR_ID := ID;
		ELSE
			X.S_BUF.BUFFER[index] := ID; (* ID *)
			index := index + 1;
			X.S_BUF.BUFFER[index] := BYTE#6; (* TYPE REAL *)
			index := index + 1;
			X.S_BUF.BUFFER[index] := INT_TO_BYTE(len_str); (* Anzahl der Bytes *)
			index := index + 1;
			_STRING_TO_BUFFER(STR:=IN,POS:=index,PT:=ADR(X.S_BUF.BUFFER),SIZE:=X.BUF_SIZE);
			index := index + len_str;
		END_IF;
	ELSIF X.STATE = BYTE#2 THEN (* Daten lesen *)
		index2 := index + 1; (* PCWORX Workaround *)
		IF X.R_BUF.BUFFER[index] <> ID OR X.R_BUF.BUFFER[index2] <> BYTE#6 THEN
			X.ERROR_ID := ID;
		ELSE
			index := index + 2;
			len_str := BYTE_TO_INT(X.R_BUF.BUFFER[index]);
			index2 := index + 1;
			index := index2 + len_str;
			IF len_STR > 0 THEN
				OUT := BUFFER_TO_STRING(PT:=ADR(X.R_BUF.BUFFER),SIZE:=X.BUF_SIZE,START:=TO_UINT(index2),STOP:=TO_UINT(index - 1));
			ELSE
				OUT := '';
			END_IF;
		END_IF;
	END_IF;
	X.index := index;
END_IF;

(* revision history
ks	15.may. 2011	rev 1.0
	original version

*)]]></ST>
    </Implementation>
    <LineIds Name="NET_VAR_STRING">
      <LineId Id="24" Count="45" />
    </LineIds>
  </POU>
</TcPlcObject>